OUTPUT_FORMAT("elf32-littlehexagon", "elf32-bighexagon",
	      "elf32-littlehexagon")
OUTPUT_ARCH(hexagon)
ENTRY(start)

PHDRS
{
    START               PT_LOAD;
    TEXT                PT_LOAD;
    TEXT_COMPRESS       PT_NULL;
    FINI                PT_LOAD;
    RODATA              PT_LOAD;
    RODATA_COMPRESS     PT_NULL;
    EH_FRAME            PT_LOAD;
    DATA                PT_LOAD;
    BSS                 PT_LOAD;
    LAST                PT_LOAD;
}


SECTIONS
{
  .START : {} : START

  /* Code starts. */
  .start          :
  {
    KEEP (*(.start))
  } =0x00c0007f
  . = ALIGN(4K);

  .init :
  {
    KEEP (*(.init))
  } =0x00c0007f

  .TEXT : {} : TEXT

  // the clade configuration is in the startup code,
  // these .o files can't be compressed
  .text           :
  {
    *crt0_standalone.o(.text*)
    *crt0.o(.text*)
  } =0x00c0007f

  old = .;
  . = 0xE0000000;
  __clade_region_high_pd0_start__ = .;
  .comp_text(COMPRESS) :
  {
    *(.text.unlikely .text.*_unlikely)
    *(.text.hot .text.hot.* .gnu.linkonce.t.hot.*)
    *(.text .stub .text.* .gnu.linkonce.t.*)
    . = ALIGN(4);
  } :TEXT_COMPRESS=0x00c0007f
  . = old;

  .fini           :
  {
    KEEP (*(.fini))
  } :FINI=0x00c0007f
  PROVIDE (__etext = .);
  PROVIDE (_etext = .);
  PROVIDE (etext = .);


  . = ALIGN(4K);
  /* Constants start. */
  .rodata         :
  {
    *crt0_standalone.o(.rodata*)
    *crt0.o(.rodata*)
  } : RODATA

  old = .;
  . = 0xE4000000;
  __clade_region_low_pd0_start__ = .;
  .comp_rodata(COMPRESS):
  {
    *(.rodata.hot .rodata.hot.* .gnu.linkonce.r.hot.*)
    *(.rodata .rodata.* .gnu.linkonce.r.*)
    . = ALIGN(4);
  } :RODATA_COMPRESS
  . = old;

  .eh_frame_hdr   :  { *(.eh_frame_hdr) } : EH_FRAME
  .eh_frame       :   { KEEP (*(.eh_frame)) }
  .gcc_except_table   :  { *(.gcc_except_table .gcc_except_table.*) }
  . = ALIGN(4K);
  .ctors          :
  {
    KEEP (*crtbegin.o(.ctors))
    KEEP (*crtbegin?.o(.ctors))
    KEEP (*(EXCLUDE_FILE (*crtend.o *crtend?.o fini.o) .ctors))
    KEEP (*(SORT(.ctors.*)))
    KEEP (*(.ctors))
  } :DATA
  .dtors          :
  {
    KEEP (*crtbegin.o(.dtors))
    KEEP (*crtbegin?.o(.dtors))
    KEEP (*(EXCLUDE_FILE (*crtend.o *crtend?.o fini.o) .dtors))
    KEEP (*(SORT(.dtors.*)))
    KEEP (*(.dtors))
  }

  .data           :
  {
    *(.data.hot .data.hot.* .gnu.linkonce.d.hot.*)
    *(.data .data.* .gnu.linkonce.d.*)
    SORT(CONSTRUCTORS)
  }
  _edata = .; PROVIDE (edata = .);
  . = ALIGN (64);
  /* Small data start. */
  . = ALIGN(4K);
  .sdata          :
  {
    PROVIDE (_SDA_BASE_ = .);
    *(.sdata.1 .sdata.1.* .gnu.linkonce.s.1.*)
    *(.sbss.1 .sbss.1.* .gnu.linkonce.sb.1.*)
    *(.scommon.1 .scommon.1.*)
    *(.sdata.2 .sdata.2.* .gnu.linkonce.s.2.*)
    *(.sbss.2 .sbss.2.* .gnu.linkonce.sb.2.*)
    *(.scommon.2 .scommon.2.*)
    *(.sdata.4 .sdata.4.* .gnu.linkonce.s.4.*)
    *(.sbss.4 .sbss.4.* .gnu.linkonce.sb.4.*)
    *(.scommon.4 .scommon.4.*)
    *(.lit[a4] .lit[a4].* .gnu.linkonce.l[a4].*)
    *(.sdata.8 .sdata.8.* .gnu.linkonce.s.8.*)
    *(.sbss.8 .sbss.8.* .gnu.linkonce.sb.8.*)
    *(.scommon.8 .scommon.8.*)
    *(.lit8 .lit8.* .gnu.linkonce.l8.*)
    *(.sdata.hot .sdata.hot.* .gnu.linkonce.s.hot.*)
    *(.sdata .sdata.* .gnu.linkonce.s.*)
  }


  .sbss           :
  {
    PROVIDE (__sbss_start = .);
    PROVIDE (___sbss_start = .);
    *(.dynsbss)
    *(.sbss.hot .sbss.hot.* .gnu.linkonce.sb.hot.*)
    *(.sbss .sbss.* .gnu.linkonce.sb.*)
    *(.scommon .scommon.*)
    /* . = ALIGN (. != 0 ? 64 : 1); */
    PROVIDE (__sbss_end = .);
    PROVIDE (___sbss_end = .);
  }


  . = ALIGN (64);
  __bss_start = .;
  .bss            :
  {
   *(.dynbss)
   *(.bss.hot .bss.hot.* .gnu.linkonce.b.hot.*)
   *(.bss .bss.* .gnu.linkonce.b.*)
   *(COMMON)
  } : BSS
  _end = .;

  /* CLADE compress section rules -- START */
  . = ALIGN (64K);
  .clade.comp : {
    __clade_comp_pd0_start__ = .;
    *(.clade.comp)
    __clade_comp_pd0_end__ = .;
  } :LAST

    . = ALIGN(4K);
  .clade.exception_low_large : {
    __clade_exception_low_large_pd0_start__ = .;
    *(.clade.exception_low_large)
    __clade_exception_low_large_pd0_end__ = .;
  } :LAST

  .clade.exception_low_small : {
    __clade_exception_low_small_pd0_start__ = .;
    *(.clade.exception_low_small)
    __clade_exception_low_small_pd0_end__ = .;
  } :LAST

  .clade.exception_high : {
    __clade_exception_high_pd0_start__ = .;
    *(.clade.exception_high)
    __clade_exception_high_pd0_end__ = .;
  } :LAST

  .clade.dict : {
    __clade_dict_pd0_start__ = .;
    *(.clade.dicts)
    __clade_dict_pd0_end__ = .;
  } :LAST

    . = ALIGN(16);
  .clade.metadata : {
    __clade_metadata_pd0_start__ = .;
    *(.clade.metadata)
    __clade_metadata_pd0_end__ = .;
  } :LAST

  /* CLADE compress section rules -- END */

  . = ALIGN (64);
  PROVIDE (end = .);
  .comment       0 :  { *(.comment) }
  /* DWARF debug sections.
     Symbols in the DWARF debugging sections are relative to the beginning
     of the section so we begin them at 0.  */
  /* DWARF 1 */
  .debug          0 :  { *(.debug) }
  .line           0 :  { *(.line) }
  .debug_aranges  0 :  { *(.debug_aranges) }
  .debug_pubnames 0 :  { *(.debug_pubnames) }
  /* DWARF 2 */
  .debug_info     0 :  { *(.debug_info .gnu.linkonce.wi.*) }
  .debug_abbrev   0 :  { *(.debug_abbrev) }
  .debug_line     0 :  { *(.debug_line) }
  .debug_frame    0 :  { *(.debug_frame) }
  .debug_str      0 :  { *(.debug_str) }
  .debug_loc      0 :  { *(.debug_loc) }
  /* DWARF 3 */
  .debug_pubtypes 0 :  { *(.debug_pubtypes) }
  .debug_ranges   0 :  { *(.debug_ranges) }
  /DISCARD/       :  { *(.note.GNU-stack) *(.gnu_debuglink) }
}
